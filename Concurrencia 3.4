programa Practica3Ejercicio4
procesos
  proceso accion (E avI: numero E caI: numero E soy: numero ES aux: numero)
  comenzar
    BloquearEsquina(10,10)
    Pos(10,10)
    si HayFlorEnLaEsquina
      tomarFlor
      BloquearEsquina(11,11)
      Pos(11,11)
      LiberarEsquina(10,10)
      depositarFlor
      Pos(avI,caI)
      LiberarEsquina(11,11)
    sino
      aux:= 1
      Pos(avI,caI)
      LiberarEsquina(10,10)
  fin
areas
  area1: AreaC(10,10,10,10)
  area2: AreaC(11,11,11,11)
  areaR1: AreaP(9,9,9,9)
  areaR2: AreaP(9,10,9,10)
  areaR3: AreaP(9,11,9,11)
  areaR4: AreaP(9,12,9,12)
  areaJefe: AreaP(1,1,1,1)
robots
  robot robot1
  variables
    caI,avI,soy,aux: numero
  comenzar
    aux:= 0
    caI:= PosCa
    avI:= PosAv
    RecibirMensaje(soy,RJ)
    mientras (aux= 0)
      accion(avI,caI,soy,aux)
    EnviarMensaje(soy,RJ)
  fin
  robot robotJefe
  variables
    ultimo,avI,caI: numero
  comenzar
    avI:= PosAv
    caI:= PosCa
    EnviarMensaje(1,R1)
    EnviarMensaje(2,R2)
    EnviarMensaje(3,R3)
    EnviarMensaje(4,R4)
    repetir 4
      RecibirMensaje(ultimo,*)
    Informar(ultimo)
    BloquearEsquina(11,11)
    Pos(11,11)
    mientras HayFlorEnLaEsquina
      tomarFlor
    Pos(avI,caI)
    LiberarEsquina(11,11)
  fin
variables
  R1: robot1
  R2: robot1
  R3: robot1
  R4: robot1
  RJ: robotJefe
comenzar
  AsignarArea(R1, area1)
  AsignarArea(R2, area1)
  AsignarArea(R3, area1)
  AsignarArea(R4, area1)
  AsignarArea(R1, area2)
  AsignarArea(R2, area2)
  AsignarArea(R3, area2)
  AsignarArea(R4, area2)
  AsignarArea(R1, areaR1)
  AsignarArea(R2, areaR2)
  AsignarArea(R3, areaR3)
  AsignarArea(R4, areaR4)
  AsignarArea(RJ, areaJefe)
  AsignarArea(RJ, area2)
  Iniciar(R1, 9,9)
  Iniciar(R2, 9,10)
  Iniciar(R3, 9,11)
  Iniciar(R4, 9,12)
  Iniciar(RJ, 1,1)
fin
