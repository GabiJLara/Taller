"Se organizó una competencia entre el equipo A y el equipo B. Cada equipo consta de dos robots que deben realizar por etapas el perímetro de un cuadrado de (10x10) como muestra la figura, recogiendo todas las flores.
 Cuando el primer robot de cada equipo completa su etapa (primera parte del recorrido) debe avisarle a su compañero para que continúe con la segunda etapa.
 Además, cuando un robot finaliza su etapa debe depositar en una de las flores juntadas en la esquina (20, 20).
Al finalizar la competencia, un robot fiscalizador debe informar la cantidad de flores del equipo que juntó más flores.
El equipo A inicia en las esquinas (1,1) y (11,10). El equipo B inicia en (13,1) y (23,10). El fiscalizador inicia en (30, 1)."

programa examenTurnoFT1
procesos
  proceso recorrerL(ES flor: numero)
  comenzar
    repetir 2
      repetir 9
        juntarFlores(flor)
        mover
      derecha
    juntarFlores(flor)
  fin
  proceso juntarFlores (ES flor: numero)
  comenzar
    mientras HayFlorEnLaEsquina
      tomarFlor
      flor:= flor + 1
  fin
  proceso girar (E cant: numero)
  comenzar
    repetir cant
      derecha
  fin
  proceso depositarTodo (E flor: numero E avI: numero E caI: numero)
  comenzar
    repetir flor
      BloquearEsquina(20,20)
      Pos(20,20)
      depositarFlor
      Pos(avI,caI)
      LiberarEsquina(20,20)
  fin
areas
  ciudad: AreaC (20,20,20,20)
  areaA1: AreaP (1,1,1,10)
  areaA11: AreaP (2,10,10,10)
  areaA2: AreaP (11,1,11,10)
  areaA22: AreaP (2,1,10,1)
  areaB1: AreaP (13,1,13,10)
  areaB11: AreaP (14,10,22,10)
  areaB2: AreaP (23,1,23,10)
  areaB22: AreaP (14,1,22,1)
  areaRJ: AreaP (30,1,30,1)
robots
  robot robot1
  variables
    flor,avI,caI: numero
  comenzar
    flor:= 0
    avI:= PosAv
    caI:= PosCa
    recorrerL(flor)
    EnviarMensaje(flor,A2)
    depositarTodo(flor,avI,caI)
  fin
  robot robot2
  variables
    flor,flores,avI,caI: numero
  comenzar
    RecibirMensaje(flores, A1)
    flor:= 0
    avI:= PosAv
    caI:= PosCa
    girar(2)
    recorrerL(flor)
    flores:= flores + flor
    EnviarMensaje(flores,RJ)
    depositarTodo(flor,avI,caI)
  fin
  robot robot3
  variables
    flor,avI,caI: numero
  comenzar
    flor:= 0
    avI:= PosAv
    caI:= PosCa
    recorrerL(flor)
    EnviarMensaje(flor,B2)
    depositarTodo(flor,avI,caI)
  fin
  robot robot4
  variables
    flor,flores,avI,caI: numero
  comenzar
    RecibirMensaje(flores, B1)
    flor:= 0
    avI:= PosAv
    caI:= PosCa
    girar(2)
    recorrerL(flor)
    flores:= flores + flor
    EnviarMensaje(flores,RJ)
    depositarTodo(flor,avI,caI)
  fin
  robot robotJefe
  variables
    max,flor: numero
  comenzar
    max:= -1
    repetir 2
      RecibirMensaje(flor,*)
      si (flor > max)
        max:= flor
    Informar(max)
  fin
variables
  A1: robot1
  A2: robot2
  B1: robot3
  B2: robot4
  RJ: robotJefe
comenzar
  AsignarArea(A1, ciudad)
  AsignarArea(A1, areaA1)
  AsignarArea(A1, areaA11)
  AsignarArea(A2, ciudad)
  AsignarArea(A2, areaA2)
  AsignarArea(A2, areaA22)
  AsignarArea(B1, ciudad)
  AsignarArea(B1, areaB1)
  AsignarArea(B1, areaB11)
  AsignarArea(B2, ciudad)
  AsignarArea(B2, areaB2)
  AsignarArea(B2, areaB22)
  AsignarArea(RJ, areaRJ)
  Iniciar(A1, 1,1)
  Iniciar(A2, 11,10)
  Iniciar(B1, 13,1)
  Iniciar(B2, 23,10)
  Iniciar(RJ, 30,1)
fin
